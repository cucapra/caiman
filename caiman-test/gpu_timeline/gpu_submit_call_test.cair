version 0.0.2

// Submits work from one function and waits for the result, then, afterwards, 
// submits work from another function and waits for the result.

// This tests two submissions, in sequential order and has two schedules
// implement the same timeline spec. Thus there is not call in the 
// timeline spec but there is a call in the schedule

// In pseudo-code:
// fn main() {
//     let r = encode simple(1);
//     let f = w.wait();
//     return f + bar();
// }

// fn bar() -> i32 {
//     return (encode simple(2)).wait();
// }

ffi i32;
native_value %i32 : i32;
ref %i32l : i32-local<flags=[map_read, map_write, copy_src, copy_dst, storage]>;
ref %i32g : i32-gpu<flags=[map_read, map_write, copy_src, copy_dst, storage]>;
event %event0;
buffer %buffer_gpu : gpu<flags = [map_read, map_write, copy_src, copy_dst, storage], alignment_bits = 0, byte_size = 1024>;
buffer_space %buff_space;

function @simple(%i32) -> %i32;
function @foo(%i32) -> %i32;
function @bar(%i32) -> %i32;

external-gpu[impl @simple] %simple(%x : i32) -> [%out : i32]
{
    path : "gpu_external.comp",
    entry : "main",
    dimensionality : 3,
    resource {
        group : 0,
        binding : 0,
        input : %x
    },
    resource {
        group : 0,
        binding : 1,
        output : %out
    }
}

value[impl @foo] %foo(%x : %i32) -> [%out: %i32] {
    %c = constant %i32 1;
    %y_t = call @simple(%c, %c, %c, %x);
    %y = extract %y_t 0;

    %y2_t = call @bar(%y);
    %y2 = extract %y2_t 0;
    return %y2;
}

value[impl @bar] %bar(%x : %i32) -> [%out: %i32] {
    %c = constant %i32 2;
    %y_t = call @simple(%c, %c, %c, %x);
    %y = extract %y_t 0;
    return %y;
}

timeline %foo_time(%e : %event0) -> [%out: %event0] {
    %enc = encoding-event %e [];
    %loc1 = extract %enc 0;
    %rem1 = extract %enc 1;
    %sub = submission-event %rem1;
    %snc = synchronization-event %loc1 %sub;

    %enc2 = encoding-event %snc [];
    %loc2 = extract %enc2 0;
    %rem2 = extract %enc2 1;
    %sub2 = submission-event %rem2;
    %snc2 = synchronization-event %loc2 %sub2;
    return %snc2;
}

spatial %foo_space(%bs : %buff_space) -> %buff_space {
    return %bs;
}

schedule[value $val = %bar, timeline $time = %foo_time, spatial $space = %foo_space]
%bar_main<$time.%snc-usable, $time.%out-usable>
(%x2_loc : $val.%x-usable $space-saved %i32l)
-> [%out : $val.%out-usable %i32] {
    %c_loc = alloc-temporary local [storage] i32;
    %x_gpu = alloc-temporary gpu [storage, copy_dst] i32;
    %y_gpu = alloc-temporary gpu [storage, map_read] i32;
    %y_loc = alloc-temporary local [map_write] i32;

    local-do-builtin $val.%c() -> %c_loc;
    %enc = begin-encoding gpu $time.%enc2 [%x_gpu, %y_gpu] [];
    encode-copy %enc %x2_loc -> %x_gpu;
    %c = read-ref i32 %c_loc;
    encode-do %enc %simple $val.%y_t(%c, %c, %c, %x_gpu) -> %y_gpu;

    %fnc = submit %enc $time.%sub2;
    sync-fence %fnc $time.%snc2;
    
    local-copy %y_gpu -> %y_loc;
    %result = read-ref i32 %y_loc;
    return %result;
}

schedule[value $val = %foo, timeline $time = %foo_time, spatial $space = %foo_space]
%foo_main<$time.%e-usable, $time.%out-usable>
(%x_loc : $val.%x-usable $space-saved %i32l)
-> [%out : $val.%out-usable %i32] {
    %c_loc = alloc-temporary local [storage] i32;
    %x_gpu = alloc-temporary gpu [storage, copy_dst] i32;
    %y_gpu = alloc-temporary gpu [storage, map_read] i32;
    %y_loc = alloc-temporary local [map_read, map_write, copy_src, copy_dst, storage] i32;

    local-do-builtin $val.%c() -> %c_loc;
    %enc = begin-encoding gpu $time.%enc [%x_gpu, %y_gpu] [];
    encode-copy %enc %x_loc -> %x_gpu;
    %c = read-ref i32 %c_loc;
    encode-do %enc %simple $val.%y_t(%c, %c, %c, %x_gpu) -> %y_gpu;

    %fnc = submit %enc $time.%sub;
    sync-fence %fnc $time.%snc;
    
    local-copy %y_gpu -> %y_loc;
    
    %join = default-join;
    %djoin = inline-join %foo_ret [] %join;
    schedule-call %bar_main[$val.%y2_t, $time, $space] (%y_loc) %djoin;
}

schedule[value $val = %foo, timeline $time = %foo_time, spatial $space = %foo_space]
%foo_ret<$time.%out-usable, $time.%out-usable>
(%x : $val.%y2-usable %i32)
-> [%out : $val.%out-usable %i32] {
    return %x;
}

pipeline "main" = %foo_main;